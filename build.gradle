plugins {
    id 'java'
    id 'com.palantir.git-version' version '0.12.3'
}

allprojects {
    version '0.7.6.1'
    ext {
        release='Stable'
        rev=versionDetails().gitHash
    }
    jar {
        onlyIf { !sourceSets.main.allSource.files.isEmpty() }
    }
}

subprojects {
    group 'com.solarisskunkwerks'
    repositories {
        mavenCentral()
    }
}

evaluationDependsOnChildren()

task copyDocs(dependsOn: subprojects.build, type: Copy) {
    from 'Docs'
    into "${buildDir}/release/SSW_${version}/Docs"
}

task copyData(dependsOn: subprojects.build, type: Copy) {
    from 'Data'
    into "${buildDir}/release/SSW_${version}/Data"
}

task copyDeps(dependsOn: subprojects.build, type: Copy) {
    from subprojects.collect { it.configurations.runtime }
    into "$buildDir/release/SSW_${version}/lib"
}

task copyJars(dependsOn: subprojects.build, type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar) }
    exclude "**/sswlib.jar"
    into "$buildDir/release/SSW_${version}"
}

task releaseBuild(dependsOn: [copyDocs, copyData, copyDeps, copyJars]) { }

task zipRelease(type: Zip, dependsOn: releaseBuild) {
    from "${buildDir}/release"
        include "**/"
    if ("$release" != "Stable") {
        archiveName "SSW_${version}-${release}-${rev}.zip"
    } else {
        archiveName "SSW_${version}.zip"
    }
}